buildscript {
    repositories {
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            url = 'https://maven.cleanroommc.com'
        }
        maven {
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        if (project.use_mixins.toBoolean()) {
            classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        }
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

if (project.use_mixins.toBoolean()) {
    apply plugin: 'org.spongepowered.mixin'
}

version = project.mod_version
group = project.maven_group
archivesBaseName = project.archives_base_name

sourceCompatibility = targetCompatibility = '1.8'

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = '1.12.2-14.23.5.2847'
    runDir = 'run'
    mappings = 'stable_39'
    def args = []
    if (project.use_coremod.toBoolean()) {
        args << '-Dfml.coreMods.load=' + coremod_plugin_class_name
    }
    if (project.use_mixins.toBoolean()) {
        args << '-Dmixin.hotSwap=true'
        args << '-Dmixin.checks.interfaces=true'
        args << '-Dmixin.debug.export=true'
    }
    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)
}

repositories {
    maven {
        url = 'https://maven.cleanroommc.com'
    }
    maven {
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        url = 'https://mvnrepository.com/artifact/org.codehaus.groovy/groovy-all'
    }
    maven {
        url 'https://dvs1.progwml6.com/files/maven/'
    }
    maven {
        name = "CurseForge"
        url = 'https://www.cursemaven.com'
    }
    maven {
        url "http://maven.tterrag.com"
    }
}

dependencies {
    if (project.use_mixins.toBoolean()) {
        deobfCompile 'zone.rong:mixinbooter:4.2'
    }

    compile 'org.jetbrains:annotations:23.0.0'

    // compile 'org.codehaus.groovy:groovy-all:3.0.11'
    compile 'org.codehaus.groovy:groovy-ant:3.0.11'
    compile 'org.codehaus.groovy:groovy-astbuilder:3.0.11'
    compile 'org.codehaus.groovy:groovy-jsr223:3.0.11'
    compile 'org.codehaus.groovy:groovy-macro:3.0.11'
    // compile 'org.codehaus.plexus:plexus-utils:3.1.0'

    compile 'mezz.jei:jei_1.12.2:4.16.1.302'
    compile 'curse.maven:crafttweaker-239197:3523955'

    if (project.debug_load_all_mods.toBoolean()) {
        deobfCompile 'curse.maven:mekanism-268560:2835175'

        // deobfCompile 'curse.maven:ender_io-64578:3328811'
        // deobfCompile 'curse.maven:endercore-231868:2972849'
        deobfProvided "com.enderio.core:EnderCore:1.12.2-+"
        deobfCompile ("com.enderio:EnderIO:1.12.2-+") {
            transitive = false
        }

        deobfCompile 'curse.maven:codechicken_lib_1_8-242818:2779848'

        deobfCompile 'curse.maven:cofh_core-69162:2920433'
        deobfCompile 'curse.maven:cofh_world-271384:2920434'
        deobfCompile 'curse.maven:redstone_flux-270789:2920436'
        deobfCompile 'curse.maven:thermal_expansion-69163:2926431'
        deobfCompile 'curse.maven:thermal_foundation-222880:2926428'

        deobfCompile 'curse.maven:immersive_engineering-231951:2974106'
    } else {
        deobfProvided 'curse.maven:mekanism-268560:2835175'

        // deobfProvided 'curse.maven:ender_io-64578:3328811'
        // deobfProvided 'curse.maven:endercore-231868:2972849'
        deobfProvided "com.enderio.core:EnderCore:1.12.2-+"
        deobfProvided ("com.enderio:EnderIO:1.12.2-+") {
            transitive = false
        }

        deobfProvided 'curse.maven:codechicken_lib_1_8-242818:2779848'

        deobfProvided 'curse.maven:cofh_core-69162:2920433'
        deobfProvided 'curse.maven:cofh_world-271384:2920434'
        deobfProvided 'curse.maven:redstone_flux-270789:2920436'
        deobfProvided 'curse.maven:thermal_expansion-69163:2926431'
        deobfProvided 'curse.maven:thermal_foundation-222880:2926428'

        deobfProvided 'curse.maven:immersive_engineering-231951:2974106'
    }

    shadow 'org.codehaus.groovy:groovy-ant:3.0.11'
    shadow 'org.codehaus.groovy:groovy-astbuilder:3.0.11'
    shadow 'org.codehaus.groovy:groovy-jsr223:3.0.11'
    shadow 'org.codehaus.groovy:groovy-macro:3.0.11'
    // shadow 'org.codehaus.plexus:plexus-utils:3.1.0'
}

sourceSets {
    main {
        ext.refMap = 'mixins.' + archives_base_name + '.refmap.json'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    rename '(.+_at.cfg)', 'META-INF/$1' // Access Transformers
}

jar {
    manifest {
        def attribute_map = [:]
        if (project.use_coremod.toBoolean()) {
            attribute_map['FMLCorePlugin'] = project.coremod_plugin_class_name
            if (project.include_mod.toBoolean()) {
                attribute_map['FMLCorePluginContainsFMLMod'] = true
            }
        }
        if (project.use_mixins.toBoolean()) {
            attribute_map['TweakClass'] = 'org.spongepowered.asm.launch.MixinTweaker'
            // false for dev and true for non dev
            attribute_map['ForceLoadAsMod'] = project.gradle.startParameter.taskNames[0] == "build"
        }
        attributes(attribute_map)
    }
}

shadowJar {
    classifier = ''
    baseName = archivesBaseName
    version = version
    configurations = [project.configurations.shadow]
    relocate 'lib.group', "${project.group}.shadow.lib.group" // ensure repackaged packages have unique names
}

reobf {
    shadowJar {}
}

// this replaces jar.finalizedBy('reobfJar') in the standard forge mod buildscript
tasks.build.dependsOn reobfShadowJar
jar.finalizedBy('reobfShadowJar')

artifacts {
    archives shadowJar
}